evaluationDependsOn(':target.p2')

apply plugin: 'org.standardout.bnd-platform'

// handle build.properties correctly
apply plugin: 'com.diffplug.gradle.eclipse.buildproperties'
apply plugin: 'com.diffplug.gradle.osgi.bndmanifest'

//////////
// JAVA //
//////////

sourceSets {
  main{
    java{
      srcDir 'src/java/'
      srcDir 'test'
    }
  }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task copyAllDependencies(type: Copy) {
    //referring to the 'compile' configuration
    from configurations.compileClasspath
    into 'jar'
}

//task copyToLib(type: Copy) {
//    from configurations.compile
//    into "$buildDir/libs"
//}
//
//build.dependsOn(copyToLib)

// Dependencies available from maven local repository
dependencies {
    implementation('p2:ummisco.gama.annotations:+')
    implementation('p2:msi.gama.processor:+')
    implementation('p2:msi.gama.core:+')

    //Used by msi.gama.core ...
    // FIX: Forcer l'inclusion dans le classpath ?
    implementation('org.jgrapht:jgrapht-core:1.0.1')
    implementation('org.jgrapht:jgrapht-ext:1.0.1')

    //Genstar packages
    implementation("org.genstar:genstar-core:latest.integration") {changing = true}
    implementation("org.genstar:genstar-spll:latest.integration")  {changing = true}
    implementation("org.genstar:genstar-spin:latest.integration") {changing = true}
    implementation("org.genstar:genstar-gospl:latest.integration") {changing = true}

    implementation("net.sf.trove4j:trove4j:+")
}

apply plugin: 'com.diffplug.gradle.eclipse.projectdeps'

////////////////////
// PLUGIN GOOMPH  //
////////////////////

// configure the OSGi bundle
jar.manifest.attributes(
        //'-exportcontents': 'core.*, gospl.*, spin.*, spll.*, gaml.*, genstar.*',
        '-removeheaders': 'Bnd-LastModified,Bundle-Name,Created-By,Tool,Private-Package,Require-Capability',
        'Conditional-Package': '!org.w3c.*, !org.xml.*, !javax.*, !org.jfree.*, !org.graphstream.*, !msi.*, *',
        'Import-Package': '!*',
        'Require-Bundle': 'msi.gama.core',
        'Export-Package': 'gama.genstar.plugin',
        'Bundle-Version': '1.0.0.${tstamp;yyyyMMddHHmm}',
        'Bundle-SymbolicName': project.name +";singleton:=true",
        'Bundle-RequiredExecutionEnvironment': 'JavaSE-1.8',
        'Require-Capability': 'osgi.ee;filter:="(&(osgi.ee=JavaSE)(version=1.8))"',
        'Bundle-Vendor': 'Gama team',
        'Bundle-License': "http://www.apache.org/licenses/LICENSE-2.0"
)

osgiBndManifest {
    copyTo 'META-INF/MANIFEST.MF'
}

